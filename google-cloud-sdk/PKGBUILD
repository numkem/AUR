# Maintainer: Sebastien Bariteau <numkem@numkem.org>
pkgname="google-cloud-sdk"
pkgver="0.9.26"
pkgrel="3"
pkgdesc="Contains tools and libraries that enable you to easily create and manage resources on Google Cloud Platform"
url="https://developers.google.com/cloud/sdk/"
license=("Apache")
arch=('i686' 'x86_64')
source=("$pkgname-$pkgver.tar.gz::https://dl.google.com/dl/cloudsdk/release/google-cloud-sdk.tar.gz"
        "profile.sh")
depends=('python2')
makedepends=('python2')
optdepends=('go: for Go version of App Engine'
            'java-environment: for Java version of App Engine'
            'php: for PHP version of App Engine')
md5sums=('4de341fcdd29a2101926269a20cbff09'
         'd7c7ccb7d32a871d67288228f5b4cd94')
options=('!strip' 'staticlibs')

build() {
  true
}

package() {
  mkdir ""$pkgdir"/opt"
  cp -r "$srcdir"/"$pkgname" "$pkgdir"/opt
  
  msg2 "Running bootstrapping script"
  python2 "$pkgdir"/opt/google-cloud-sdk/bin/bootstrapping/install.py \
  	--usage-reporting false --disable-installation-options \
  	--path-update false --bash-completion false --rc-path="$srcdir"/fake.bashrc \
    --additional-components pkg-go pkg-python pkg-java
  install -Dm755 "$pkgdir"/opt/google-cloud-sdk/completion.bash.inc "$pkgdir"/etc/bash_completion.d/google-cloud-sdk
  install -Dm755 "$srcdir"/profile.sh "$pkgdir"/etc/profile.d/google-cloud-sdk 

  msg2 "Changing python references to python2"
  find "$pkgdir"/opt -type f -exec sed -i 's|#!.*python.*|#!/usr/bin/env python2|g' {} \;
 
  msg2 "Installing man pages"
  mkdir -p "$pkgdir"/usr/share/man/man1
  mv "$pkgdir"/opt/google-cloud-sdk/help/man/man1 "$pkgdir"/usr/share/man/
  rm -rf "$pkgdir"/opt/google-cloud-sdk/help/man/
  
  msg2 "Cleaning files and folders"
  rm -rf "$pkgdir"/opt/google-cloud-sdk/.install/

  msg2 "Fixing files permissions"
  chown root:root -R "$pkgdir"
}

# Contributor: Sebastien Bariteau <numkem@gmail.com>
# Contributor: 3ED <krzysztof1987 at gmail dot com>

pkgname='perl-archive-extract'
_lastauthor=B/BI/BINGOS
_pkgname=Archive-Extract
pkgver='0.68'
pkgrel='1'
pkgdesc="Generic archive extracting mechanism"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=()
url='http://search.cpan.org/dist/Archive-Extract'
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
md5sums=('8316c72e5df9808364157875bd48d887')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install
}

# vim:set ts=2 sw=2 et:

